plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk 34

    namespace "com.audiospy"

    defaultConfig {
        applicationId "com.audiospy"
        minSdk 33
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def signingProps = new Properties()
    if (keystorePropertiesFile.exists()) {
        signingProps.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        release {
            storeFile = file(System.getenv("KEYSTORE_PATH") ?: signingProps["storeFile"] ?: "keystore.jks")
            storePassword = System.getenv("STORE_PASSWORD") ?: signingProps["storePassword"]
            keyAlias = System.getenv("KEY_ALIAS") ?: signingProps["keyAlias"]
            keyPassword = System.getenv("KEY_PASSWORD") ?: signingProps["keyPassword"]
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures { viewBinding true }
    kotlinOptions { jvmTarget = '17' }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    task printVersionName {
        doLast { println android.defaultConfig.versionName }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    ksp 'androidx.room:room-compiler:2.6.1'
}
